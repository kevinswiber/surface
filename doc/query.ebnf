/* converted from Jison to EBNF using http://bottlecaps.de/convert/ */
/* Diagrams can be generated at http://railroad.my28msec.com/rr/ui */

select-statement
         ::= 'select' fields ( where-clause )? ( orderby-clause ) ?
fields   ::= column ( ',' column )*
           | '*'
where-clause
         ::= 'where' filter
filter
         ::= boolean-predicate 
           | conjunction
           | disjunction
boolean-predicate ::= ('not')? predicate
predicate
         ::= comparison-predicate
           | contains-predicate
           | location-predicate
conjunction
         ::= filter ( and boolean-predicate )+
disjunction
         ::= filter ( or boolean-predicate )+
comparison-predicate
         ::= column comparison literal
contains-predicate
         ::= column 'contains' string
location-predicate
         ::= column 'within' location
location ::= number 'of' coordinates
coordinates
         ::= number ',' number
orderby-clause
         ::= 'order by' sort-list
sort-list
         ::= sort-expression ( ',' sort-expression )*
sort-expression
         ::= name direction
direction
         ::= ( 'asc' | 'desc' )?
comparison
          ::= '='
          | '>'
          | '>='
          | '<'
          | '<='
          | 'eq'
          | 'gt'
          | 'gte'
          | 'lt'
          | 'lte'
and
         ::= 'and'
         | '&&'
or
         ::= 'or'
         | '||'
literal  ::= number
           | string
column   ::= name
name
          ::= [A-Za-z0-9_-][\.A-Za-z0-9_-]*
          | '[' name ']'
string
          ::= '"' [^"]* '"'
          | "'" [^']* "'"
number
          ::= [+-]?[0-9]*.?[0-9]+([eE][-+]?[0-9]+)?
